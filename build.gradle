plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    id 'java-gradle-plugin'
    id 'maven'
    id 'signing'
}

group 'com.github.mictaege'
version '2021.4.2'

sourceCompatibility = 1.8

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'arete-gradle'
            implementationClass = 'com.github.mictaege.arete_gradle.AretePlugin'
        }
    }
}

wrapper {
    gradleVersion = "6.7"
    distributionType = Wrapper.DistributionType.ALL
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'org.junit.platform:junit-platform-launcher:1.7.1'
    implementation 'com.github.mictaege:arete:2021.3'
    implementation 'org.fusesource.jansi:jansi:2.3.2'
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'org.freemarker:freemarker:2.3.31'
}

task generateResources {
    ext {
        propFile = file("$buildDir/generated/arete-gradle.properties")
    }
    outputs.file propFile
    doLast {
        mkdir propFile.parentFile
        propFile.text = "version=$project.version"
    }
}
processResources {
    from files(generateResources)
}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") } //sign only when upload archives
    sign configurations.archives
}

//don't refer to username and pwd directly so it could be executed in another environment with another _gradle.properties_
String publishUsername = hasProperty("ossrhUsername") ? property("ossrhUsername") : ""
String publishPassword = hasProperty("ossrhPassword") ? property("ossrhPassword") : ""

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: publishUsername, password: publishPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: publishUsername, password: publishPassword)
            }

            pom.project {
                name 'arete-gradle'
                packaging 'jar'
                description 'This repository contains the Arete Gradle Plugin. For using Arete Gradle plugin see the https://github.com/mictaege/arete-gradle documentation.'
                url 'https://github.com/mictaege/arete-gradle'
                scm {
                    connection 'https://github.com/mictaege/arete-gradle.git'
                    url 'https://github.com/mictaege/arete-gradle'
                }
                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id 'mictaege'
                        name 'Michael Taege'
                    }
                }
            }
        }
    }
}